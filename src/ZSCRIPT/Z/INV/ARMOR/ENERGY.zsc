extend
class ZArmor{
	int OldStats;

	protected
	int //General
		Recharger,
		
		//Energy
		Energy,
		MaxEnergy,
		IdleRechargeBonus,
		EnergyRechargeRate,
		EnergyRechargeAmount,
		
		//Magnetic Repulsion
		MagnetRechargeRate,
		MagnetRechargeAmount,
		
		//Plasma Shield
		ShieldRechargeRate,
		ShieldRechargeAmount,
		
		//Hard Light Construct
		LightChargeUp,
		LightRechargeRate,
		LightRechargeAmount;

	virtual
	clearscope
	int, int GetEnergy()
	{return Energy, MaxEnergy;}
	
	virtual
	clearscope
	int, int GetMagnet()
	{return Magnet, MaxMagnet;}
	
	virtual
	clearscope
	int, int GetShield()
	{return Shield, MaxShield;}
	
	virtual
	clearscope
	int, int GetLight()
	{return Light, MaxLight;}
	
	virtual
	void DrainEnergy(int Amount, ZPlayer Player=null){
		if(Amount<=Energy)
			Energy-=Amount;
		else if(Player){
			int temp=Energy;
			Energy=0;
			Player.DrainEnergy(Amount-temp);
		}
	}
	
	virtual
	void RechargeEnergy(ZPlayer Player){
		if(!Player || Player.Health<=0)return;
		//This Tick
		int CurrentStats=Player.GetEnergy()
						+Player.GetPlate()
						+Player.GetMagnet()
						+Player.GetShield()
						+Player.GetLight();
		if(CurrentStats>=OldStats){
			int ShieldGoal;
			if(Energy<MaxEnergy){
				if(Recharger==EnergyRechargeRate){
					Energy=Clamp(Energy+EnergyRechargeAmount*(Player.Vel==(0,0,0)?IdleRechargeBonus:1), 0, MaxEnergy);
					if(Energy==MaxEnergy)Recharger=0;
				}else Recharger++;
			}else if((Info & AMR_MAGNET) && (Info & AMR_MOWNPOWER) && Magnet<MaxMagnet){
				if(Recharger==MagnetRechargeRate){
					Magnet=Clamp(Magnet+MagnetRechargeAmount*(Player.Vel==(0,0,0)?IdleRechargeBonus:1), 0, MaxMagnet);
					if(Magnet==MaxMagnet)Recharger=0;
				}else Recharger++;
			}else if((Info & AMR_SHIELD) && (Info & AMR_SOWNPOWER) && Shield<(ShieldGoal=int(Shield/ShieldDivider+1)*ShieldDivider)){
				if(Recharger==ShieldRechargeRate){
					Shield=Clamp(Shield+ShieldRechargeAmount*(Player.Vel==(0,0,0)?IdleRechargeBonus:1), 0, ShieldGoal);
					if(Shield==ShieldGoal)Recharger=0;
				}else Recharger++;
			}else if((Info & AMR_LIGHT) && (Info & AMR_LOWNPOWER) && Light==0){
				if(LightChargeUp==MaxLight){
					Light=MaxLight;
					Recharger=LightChargeUp=0;
				}else if(Recharger==LightRechargeRate)
					LightChargeUp=Clamp(LightChargeUp+LightRechargeAmount*(Player.Vel==(0,0,0)?IdleRechargeBonus:1), 0, MaxLight);
				else Recharger++;
			}
		}else Recharger=0;
		//Last Tick
		OldStats=Player.GetEnergy()
				+Player.GetPlate()
				+Player.GetMagnet()
				+Player.GetShield()
				+Player.GetLight();
	}
}