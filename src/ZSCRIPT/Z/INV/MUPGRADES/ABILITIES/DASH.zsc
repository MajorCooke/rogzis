class ZMAU_BoostDash:ZMArmorUpgrade{
    transient
    int DashCountdown;

    int DashCost,
        DashPower,
        DashCooldown;

    override
    ZMArmorUpgrade Init(){
        Parser_Zinf Config=ZManager.Config();
        DashCost=Config.GetInt("ABILITIES", "iDASH_COST");
        DashPower=Config.GetInt("ABILITIES", "iDASH_POWER");
        DashCooldown=Config.GetInt("ABILITIES", "iDASH_COOLDOWN");
        return self;
    }

    override
    void Tick(ZPlayer Player)
    {if(DashCountdown>0)DashCountdown--;}

    override
    void HandleMovement(ZPlayer Player, PlayerInfo PlayerInfo){
        ZMarine Marine=ZMarine(Player);
        if(!Marine)return;
        if(DashCountdown==0){
            static const int DashKeys[]={
                BT_FORWARD,
                BT_BACK,
                BT_MOVELEFT,
                BT_MOVERIGHT
            };
            ZMStats Stats=Marine.MStats;
            for(int i=PlayerInfo.OnGround; i<DashKeys.Size(); i++){
                if((Marine.NewButtons & BT_SPEED) && (!PlayerInfo.OnGround || (~Marine.Buttons & BT_FORWARD)) && (Marine.Buttons & DashKeys[i]) &&
                   (Stats.Energy>DashCost)){
                    if(Marine.NewButtons & BT_SPEED){
                        Stats.Energy-=DashCost;
                        int MidAirBoost=DashPower*!PlayerInfo.OnGround,
                            Power=(PlayerInfo.OnGround?2:1)*DashPower*ZCalc.Alternate(i);
                        Marine.Vel.xy+=Power*((i>1)?-Marine.SideVector:Marine.ViewVector.xy);
                        Marine.Vel.z+=MidAirBoost;
                        DashCountdown=Stats.DashCooldown;
                    }
                    return;
                }
            }
        }
    }
}