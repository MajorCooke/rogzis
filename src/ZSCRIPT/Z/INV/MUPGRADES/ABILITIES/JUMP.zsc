class ZMAU_BoostJump:ZMArmorUpgrade{
    transient
    int DashCountdown;

    int DashCost,
        DashPower,
        DashCooldown;

    override
    ZMArmorUpgrade Init(){
        Parser_Zinf Config=ZManager.Config();
        DashCost=Config.GetInt("ABILITIES", "iDASH_COST");
        DashPower=Config.GetInt("ABILITIES", "iDASH_POWER");
        DashCooldown=Config.GetInt("ABILITIES", "iDASH_COOLDOWN");
        return self;
    }

    override
    void Tick(ZPlayerBase Player)
    {if(DashCountdown>0)DashCountdown--;}

    override
    void HandleMovement(ZPlayerBase Player, PlayerInfo PlayerInfo){
        ZMarine Marine=ZMarine(Player);
        if(!Marine)return;
        ZMStats Stats=Marine.MStats;
        if(Marine.Buttons & BT_JUMP){
            if(!PlayerInfo.OnGround && DashCountdown==0 && (~Marine.OldButtons & BT_JUMP) && Stats.Energy>DashCost){
                Stats.Energy-=DashCost;
                Marine.Vel.Z+=DashPower;
                DashCountdown=DashCooldown;
            }
            if(Level.IsJumpingAllowed() && PlayerInfo.OnGround && PlayerInfo.JumpTics==0)DashCountdown=0;
        }
    }
}