class ZElement ui{
    protected
    Vector2 __Scale__;

    protected
    ZComponent Parent;

    ZRectangle AABB;

    static
    ZElement Create(Vector2 Pos, Vector2 Size, ZComponent Parent=null, Class<ZElement> ClassName='ZElement'){
        ZElement Result=ZElement(new(ClassName));
        Result.__Scale__=(1, 1);
        if(Parent){
            Result.Parent=Parent;
            Parent.Elements.Push(Result);
            Result.__Scale__=Parent.__Scale__;
        }
        Result.AABB=ZRectangle.Create(Pos, Size);
        return Result;
    }

    double Height()
    {return AABB.Height();}

    double Width()
    {return AABB.Width();}

    ZRectangle Bounds()
    {return AABB.Clone();}

    Vector2 Scale()
    {return __Scale__;}

    ZRectangle AbsBounds()
    {return ZRectangle.Create(AbsPos(), Rel2AbsPos(AABB.Pos+AABB.Size)-AbsPos());}

    Vector2 ScaledScreenSize()
    {return (Screen.GetWidth()/__Scale__.X, Screen.GetHeight()/__Scale__.Y);}

    Vector2 AbsPos()
    {return (Parent)?Parent.Rel2AbsPos(AABB.Pos):Screen.VirtualToRealCoords(AABB.Pos, ZCalc.ScreenSize(), ScaledScreenSize());}

    Vector2 Rel2AbsPos(Vector2 DesPos)
    {return Screen.VirtualToRealCoords(Rel2VirtualPos(DesPos), ZCalc.ScreenSize(), ScaledScreenSize());}

    Vector2 Rel2VirtualPos(Vector2 DesPos)
    {return (Parent)?Parent.Rel2VirtualPos(AABB.Pos+DesPos):DesPos;}

    void OnDraw(){
        int Temp1, Temp2, Temp3, Temp4=Screen.GetClipRect();
        ZDrawer.SetClipRect(AbsBounds());
        Draw();
        Screen.SetClipRect(Temp1, Temp2, Temp3, Temp4);
    }

    virtual
    void Draw(){}

    virtual
    void Tick(){}

    virtual
    void OnUIEvent(UIEvent e){}

    virtual
    void OnInputEvent(InputEvent e){}
}