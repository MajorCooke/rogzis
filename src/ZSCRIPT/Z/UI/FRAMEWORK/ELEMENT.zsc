class ZElement ui abstract{
    protected
    Vector2 __Scale__;

    protected
    ZComponent Parent;

    ZDrawer Drawer;

    ZRectangle AABB;

    static
    ZElement Create(Vector2 Pos, Vector2 Size, ZComponent Parent=null, Class<ZElement> ClassName='ZElement'){
        ZElement Result=ZElement(new(ClassName));
        Result.__Scale__=(1, 1);
        if(Parent){
            Result.Parent=Parent;
            Parent.Elements.Push(Result);
            Result.__Scale__=Parent.__Scale__;
        }
        Result.AABB=ZRectangle.Create(Pos, Size);
        Result.Drawer=ZDrawer.Create(Result);
        return Result;
    }

    double Height()
    {return AABB.Height();}

    double Width()
    {return AABB.Width();}

    ZRectangle Bounds()
    {return AABB.Clone();}

    Vector2 Scale()
    {return __Scale__;}

    Vector2 ScaledScreenSize()
    {return (Screen.GetWidth()/__Scale__.X, Screen.GetHeight()/__Scale__.Y);}

    Vector2 AbsPos()
    {return (Parent)?Parent.Rel2AbsPos(AABB.Pos):AABB.Pos;}

    Vector2 Rel2AbsPos(Vector2 DesPos)
    {return (Parent)?Parent.Rel2AbsPos(AABB.Pos+(DesPos.X*__Scale__.X, DesPos.Y*__Scale__.Y)):AABB.Pos+(DesPos.X*__Scale__.X, DesPos.Y*__Scale__.Y);}

    void OnDraw()
    {Draw();}

    virtual
    void Draw(){}

    virtual
    void Tick(){}

    virtual
    void OnUIEvent(UIEvent e){}

    virtual
    void OnInputEvent(InputEvent e){}
}