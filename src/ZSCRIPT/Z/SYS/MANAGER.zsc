class ZManager:Thinker{
    static
    ZManager Instance(){
        ThinkerIterator it=ThinkerIterator.Create("ZManager", STAT_STATIC);
        ZManager p=ZManager(it.Next());
        if(!p)p=new("ZManager").Init();
        return p;
    }

    private
    ZManager Init(){
        ChangeStatNum(STAT_STATIC);
        //World State
        WorldState=new("ZWorldState");
        InitRegistries();
        return self;
    }

    static
    clearscope
    void Debug(String Output, Name CVarName)
    {if(CVar.FindCVar(CVarName).GetBool())console.printf(Output);}

    static
    clearscope
    void Error(String Error, ZErrorLevel Level){
        switch(Level){
            case ERR_INFO:break;
            case ERR_WARNING:
                Console.PrintF("\cx%s", Error);
                break;
            case ERR_ERROR:
                Console.PrintF("\cr%s", Error);
                break;
            case ERR_FATAL:
                ThrowAbortException("\cy%s", Error);
                break;
            default:
                ThrowAbortException("Unclassified Error: '\cw%s'", Error);
                break;
        }
    }
}