class ZMarine:ZPlayer{
    [Property]{
        [Player]
        // Descriptors
        DisplayName="Marine";
        AttackZOffset=13;
        // Start Items
        StartItem="ZWeaponSystem";
        StartItem="Fist";
        StartItem=("Clip", 50);
        // Weapons
        WeaponSlot=(1, "Fist");
        WeaponSlot=(2, "ZWeaponSystem");
    }

    override
    void PreTick(){
        super.PreTick();
        MStats.ArmorTick(self);
    }

    override
    void UpdateStats(){
        [Health, MaxHealth, RunHealth]=MStats.GetHealth();
        A_SetHealth(Health);
        [ForwardMove1, ForwardMove2]=MStats.GetForwardMove();
        [SideMove1, SideMove2]=MStats.GetSideMove();
        ViewBob=MStats.ViewBob;
        JumpZ=MStats.JumpZ;
    }

    override
    void SetAimTarget(){
        FLineTraceData Data;
        Trace(Data, 2048, Stats.ActualPitch);
        AimTarget=Data.HitActor;
    }

    override
    void HandleScreenShake(){
        AddScreenShake(ZCalc_S.NormRangeMap(Vel.Z-LastVel.Z, 35, 50));
        double CurrentShake=MStats.ShakeLevel*Clamp(ZCalc_S.SmoothStart(Stats.Shake), 0, 1);
        if(CurrentShake>0){
            ActiveYaw=CurrentShake*Perl2Shake.RangeMap(YawShake.Get(Level.MapTime%1000, 1));
            ActivePitch=CurrentShake*Perl2Shake.RangeMap(PitchShake.Get(Level.MapTime%1000, 1));
            ActiveRoll=2*CurrentShake*Perl2Shake.RangeMap(RollShake.Get(Level.MapTime%1000, 1));
            AddScreenShake(-.02);
        }else ActiveYaw=ActivePitch=ActiveRoll=0;
    }

    override
    int DamageMobj(Actor Inflictor, Actor Source, int Damage, Name Mod, int Flags, double Angle){
        if(Damage>0)MStats.Recharger=0;
        Damage=MStats.ModifyDamageTaken(Inflictor, Source, Damage, Mod, Flags, Angle);
        Damage=super.DamageMobj(Inflictor, Source, Damage, Mod, Flags|DMG_NO_ARMOR|DMG_NO_FACTOR, Angle);
        return Damage;
    }

    override
    int TakeSpecialDamage(Actor Inflictor, Actor Source, int Damage, Name DamageType){
        MStats.Health-=Damage;
        Damage=(Damage>=TELEFRAG_DAMAGE || MStats.Health<=0)?Damage:0;
        return Damage;
    }
}

#include "ZSCRIPT/Z/PLYR/MARINE/STATS.zsc"
#include "ZSCRIPT/Z/PLYR/MARINE/MOVEMENT.zsc"