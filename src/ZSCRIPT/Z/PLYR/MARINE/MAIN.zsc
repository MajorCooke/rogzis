class ZMarine:ZPlayer{
    Default{
        //Descriptors
        Player.DisplayName "Marine";
        Player.AttackZOffset 13;
        //Start Items
        Player.StartItem "Pistol";
        Player.StartItem "Fist";
        Player.StartItem "Clip", 50;
        //Weapons
        Player.WeaponSlot 1, "Fist";
        Player.WeaponSlot 2, "Pistol";
    }

    override
    void PostBeginPlay(){
        super.PostBeginPlay();
        MaxHealth=MStats.MaxHealth;
    }

    override
    void PreTick(){
        super.PreTick();
        MStats.ArmorTick(self);
        MStats.Recharge(self);
    }

    override
    void PostTick(){
        HandleView();
        super.PostTick();
    }

    override
    void UpdateStats(){
        A_SetHealth(MStats.Health);
        MaxHealth=MStats.MaxHealth;
        RunHealth=MStats.RunHealth;
        ForwardMove1=MStats.ForwardMove1;
        ForwardMove2=MStats.ForwardMove2;
        SideMove1=MStats.SideMove1;
        SideMove2=MStats.SideMove2;
        ViewBob=MStats.ViewBob;
    }

    override
    void SetAimTarget(){
        FLineTraceData Data;
        Trace(Data, 2048, Stats.ActualPitch);
        AimTarget=Data.HitActor;
    }

    override
    bool Trace(out FLineTraceData Data, double Distance=1024, double Pitch=0)
    {return LineTrace(Stats.ActualYaw, Distance, Pitch, Data: Data);}

    override
    int DamageMobj(Actor Inflictor, Actor Source, int Damage, Name Mod, int Flags=0, double Angle=0){
        Damage=MStats.ModifyDamage(Inflictor, Source, Damage, Mod, Flags, Angle);
        Damage=super.DamageMobj(Inflictor, Source, Damage, Mod, Flags|DMG_NO_ARMOR|DMG_NO_FACTOR, Angle);
        return Damage;
    }

    override
    int TakeSpecialDamage(Actor Inflictor, Actor Source, int Damage, Name DamageType){
        MStats.Health-=Damage;
        return 0;
    }

    override
    void CheatGive(String Name, int Amount)
    {console.printf("WIP");}
}

#include "ZSCRIPT/Z/PLYR/MARINE/VIEW.zsc"
#include "ZSCRIPT/Z/PLYR/MARINE/STATS.zsc"
#include "ZSCRIPT/Z/PLYR/MARINE/MOVEMENT.zsc"