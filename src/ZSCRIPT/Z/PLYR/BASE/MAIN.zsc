class ZPlayer:PlayerPawn{
    transient
    int Buttons,
        OldButtons,
        NewButtons,
        SameButtons;

    transient
    double ActiveYaw,
           ActivePitch,
           ActiveRoll,
           PassiveRoll,
           RunOscillation;

    transient
    Vector2 SideVector;

    transient
    Vector3 ViewVector;

    transient
    Actor AimTarget;

    transient
    ZPerlin YawShake,
            PitchShake,
            RollShake;

    transient
    ZStats Stats;

    transient
    ZMStats MStats;

    Default{
        Species "Player";
    }

    override
    void PostBeginPlay(){
        super.PostBeginPlay();
        //View
        Stats.ShakeLevel=1;
        double XYLength=Cos(Stats.ActualPitch=Pitch);
        Stats.ActualRoll=Roll=0;
        ViewVector=(Cos(Stats.ActualYaw=Angle)*XYLength, Sin(Stats.ActualYaw)*XYLength, -Sin(Stats.ActualPitch));
        SideVector=ZCalc.V2Rotate(ViewVector.XY, -90);
    }

    virtual
    void AddScreenShake(double Amount)
    {Stats.Shake=ZCalc.NormRange(Stats.Shake+Amount);}

    override
    void Tick(){
        super.Tick();
        //Handle Voodoo Dolls
        if(!player || !player.mo || player.mo!=self || Health<=0)return;
        PreTick();
        RealTick();
        PostTick();
    }

    virtual
    void PreTick(){
        UpdateStats();
        RecordInput();
        SetAimTarget();
    }

    virtual
    void RealTick(){}

    virtual
    void PostTick()
    {CheckCollisionEffects();}

    virtual
    void UpdateStats(){}

    virtual
    void RecordInput(){
        Buttons=Player.cmd.buttons;
        OldButtons=Player.oldbuttons;
        NewButtons=Buttons & ~OldButtons;
        SameButtons=Buttons & OldButtons;
    }

    virtual
    void SetAimTarget(){
        FLineTraceData Data;
        Trace(Data, 2048, Pitch);
        AimTarget=Data.HitActor;
    }

    override
    void CheckPitch(){}

    virtual
    bool Trace(out FLineTraceData Data, double Distance=1024, double Pitch=0)
    {return LineTrace(Angle, Distance, Pitch, Data: Data);}
}

#include "ZSCRIPT/Z/PLYR/BASE/VIEW.zsc"
#include "ZSCRIPT/Z/PLYR/BASE/STATS.zsc"
#include "ZSCRIPT/Z/PLYR/BASE/MOVEMENT.zsc"