class ZPlayer:PlayerPawn{
    int Buttons,
        OldButtons,
        NewButtons,
        SameButtons;

    double ActiveYaw,
           ActivePitch,
           ActiveRoll,
           PassiveRoll,
           RunOscillation,
           RelativeZ;

    Vector2 SideVector,
            ViewVectorXY;

    Vector3 ViewVector;

    Actor AimTarget;

    ZPerlin YawShake,
            PitchShake,
            RollShake;

    ZStats Stats;

    ZMStats MStats;

    Default{
        Species "Player";
    }

    override
    void PostBeginPlay(){
        super.PostBeginPlay();
        UpdateStats();
        Stats.ActualPitch=Pitch;
        Stats.ActualYaw=Angle;
        Stats.ActualRoll=Roll;
    }

    virtual
    void AddScreenShake(double Amount)
    {Stats.Shake=ZCalc.NormRange(Stats.Shake+Amount);}

    override
    void Tick(){
        super.Tick();
        //Handle Voodoo Dolls
        if(!player || !player.mo || player.mo!=self || Health<=0)return;
        PreTick();
        RealTick();
        PostTick();
    }

    virtual
    void PreTick(){
        UpdateStats();
        RecordInput();
        SetAimTarget();
        SetRelativeZ();
    }

    virtual
    void RealTick(){}

    virtual
    void PostTick(){
        HandleView();
        CheckCollisionEffects();
    }

    virtual
    void UpdateStats(){}

    virtual
    void RecordInput(){
        Buttons=Player.cmd.buttons;
        OldButtons=Player.oldbuttons;
        NewButtons=Buttons & ~OldButtons;
        SameButtons=Buttons & OldButtons;
    }

    virtual
    void SetAimTarget(){
        FLineTraceData Data;
        Trace(Data, 2048, Stats.ActualPitch);
        AimTarget=Data.HitActor;
    }

    virtual
    void SetRelativeZ()
    {RelativeZ=Pos.Z-GetZAt();}

    override
    void CheckPitch(){}

    virtual
    bool Trace(out FLineTraceData Data, double Distance=1024, double Pitch=0)
    {return LineTrace(Stats.ActualYaw, Distance, Pitch, Data: Data);}
}

#include "ZSCRIPT/Z/PLYR/BASE/VIEW.zsc"
#include "ZSCRIPT/Z/PLYR/BASE/STATS.zsc"
#include "ZSCRIPT/Z/PLYR/BASE/MOVEMENT.zsc"