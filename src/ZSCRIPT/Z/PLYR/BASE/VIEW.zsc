extend
class ZPlayerBase{
	protected
	transient
	double ActiveYaw,
		   ActivePitch,
		   ActiveRoll,
		   PassiveRoll,
		   Shake,
		   ShakeLevel,
		   RunOscillation;

	transient
	Vector2 SideVector;

	transient
	Vector3 ViewVector;

	transient
	ZPerlin YawShake,
			PitchShake,
			RollShake;

    virtual
	void HandleScreenShake(){
		double CurrentShake=ShakeLevel*ZCalc.NormRange(ZCalc.SmoothStart(Shake));
		if(CurrentShake>0){
			ActiveYaw=CurrentShake*ZCalc.DRangeMap(YawShake.Get(Level.MapTime%1000, 1), 2, 7, -2.5, 2.5);
			ActivePitch=CurrentShake*ZCalc.DRangeMap(PitchShake.Get(Level.MapTime%1000, 1), 2, 7, -2.5, 2.5);
			ActiveRoll=CurrentShake*ZCalc.DRangeMap(RollShake.Get(Level.MapTime%1000, 1), 2, 7, -5, 5);
			AddScreenShake(-.025);
		}else ActiveYaw=ActivePitch=ActiveRoll=0;
	}

	virtual
	void HandleViewTilt(){
		if(Player.OnGround){
			//Walking
			PassiveRoll+=Sin(RunOscillation+=5*ZCalc.PI)*(Vel dot ViewVector)/50.;
			//Strafing
			PassiveRoll+=(Vel.XY dot SideVector)/25.;
		}
		PassiveRoll*=(abs(PassiveRoll)>.00001)?.75:0;
	}

	virtual
	void HandleViewChange(){
		UserCmd cmd=Player.cmd;
		Stats.Orientation.SetEulerAngles(Stats.ActualYaw+ZCalc.DRangeMap(cmd.Yaw, -32767, 32767, -128, 128), Clamp(Stats.ActualPitch-ZCalc.DRangeMap(cmd.Pitch, -32767, 32767, -128, 128), -90, 90), Stats.ActualRoll);
	}

	virtual
	void HandleOrientation(){
        //Sync Camera with ZQuaternion
		Stats.Orientation=Stats.Orientation.Unit();
		[Stats.ActualYaw, Stats.ActualPitch, Stats.ActualRoll]=Stats.Orientation.GetEulerAngles();
		Stats.ActualYaw=abs(Angle)>=179.9999?Stats.ActualYaw:ZCalc.TransferSign(Stats.ActualYaw, Angle);
		A_SetAngle(Stats.ActualYaw+ActiveYaw, SPF_INTERPOLATE);
		A_SetPitch(Stats.ActualPitch+ActivePitch, SPF_INTERPOLATE);
		A_SetRoll(Stats.ActualRoll+ActiveRoll+PassiveRoll, SPF_INTERPOLATE);
        //Direction Vectors
		double XYLength=Cos(Stats.ActualPitch);
		ViewVector=(Cos(Stats.ActualYaw)*XYLength, Sin(Stats.ActualYaw)*XYLength, -Sin(Stats.ActualPitch));
		SideVector=ZCalc.V2Rotate(ViewVector.XY, -90);
	}
}