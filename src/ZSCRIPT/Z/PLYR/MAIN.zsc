class ZPlayer:DoomPlayer{	
	int //Oxygen
		oxygen,
		maxOxygen;
		
	Actor AimTarget;

	//Oxygen
	Property Oxygen: oxygen;
	Property MaxOxygen: maxOxygen;
	
	Default{
		//Oxygen
		ZPlayer.Oxygen ZPlayerDefaults.MAX_OXYGEN;
		ZPlayer.MaxOxygen ZPlayerDefaults.MAX_OXYGEN;
	}
	
	override
	void PostBeginPlay(){
		super.PostBeginPlay();
		Parser_Zinf Config=ZManager.Config();
		//Player
			//Health
			A_SetHealth(Config.GetInt("HEALTH","iSTART_HEALTH"));
			MaxHealth=Config.GetInt("HEALTH","iMAX_HEALTH");
			RunHealth=floor(Config.GetInt("HEALTH","iRUN_HEALTH")*Config.GetDouble("HEALTH","dRUN_HEALTH_PERC"));
			//Inventory
			ZInv=ZInventory.Create();
			ArmorStats stts;
			stts.Energy=5000;
			ThrustStats thurst;
			thurst.ThrustPower=10;
			thurst.SprintSpeed=5;
			thurst.DashTimer=12;
			thurst.DashCooldown=50;
			ZInv.Armor[AMR_TORSO]=ZArmor.Create("", "", AMR_TORSO, AMR_ENERGY|AMR_THRUST, stts, thurst);
			ZInv.Armor[AMR_HANDS]=ZArmor.Create("", "", AMR_HANDS, AMR_ENERGY|AMR_THRUST, stts, thurst);
	}
	
	override
	void Tick(){
		//Handle Voodoo Dolls
		if (!player || !player.mo || player.mo != self) return super.Tick();
		//Get Actor being aimed at
		FTranslatedLineTarget t;
		AimLineAttack(angle, 2048, t, .5);
		AimTarget=t.linetarget;
		super.Tick();
	}
	
	virtual
	clearscope
	int, int GetEnergy(){
		int Energy, MaxEnergy,
			e,		me;
		for(int i=0; i<ZInv.Armor.Size(); i++)
				if(ZInv.Armor[i]){
					[e, me]=ZInv.Armor[i].GetEnergy();
					Energy+=e;
					MaxEnergy+=me;
				}
		return Energy, MaxEnergy;
	}
}